@Metadata.layer: #CORE

@UI: { headerInfo: { typeName: 'Travel',
                     typeNamePlural: 'Travels',
                     title: { type: #STANDARD,
                              value: 'TravelID' } } }

annotate view /DMO/C_Travel_U with

{
  @UI.facet: [ { id:            'Travel',
                 purpose:       #STANDARD,
                 type:          #IDENTIFICATION_REFERENCE,
                 label:         'Travel',
                 position:      10 },
               { id:            'Booking',
                 purpose:       #STANDARD,
                 type:          #LINEITEM_REFERENCE,
                 label:         'Booking',
                 position:      20,
                 targetElement: '_Booking'}]

  @UI: { lineItem:       [ { position: 10,
                             importance: #HIGH } ],
         identification: [ { position: 10 } ],
         selectionField: [ { position: 10 } ] }
  TravelID;

  @UI: { lineItem:       [ { position: 20,
                             importance: #HIGH } ],
         identification: [ { position: 20 } ],
         selectionField: [ { position: 20 } ] }
  AgencyID;

  @UI: { lineItem:       [ { position: 30,
                             importance: #HIGH } ],
         identification: [ { position: 30 } ],
         selectionField: [ { position: 30 } ] }
  CustomerID;

  @UI: { lineItem:       [ { position: 40,
                              importance: #MEDIUM } ],
         identification: [ { position: 40 } ] }
  BeginDate;

  @UI: { lineItem:       [ { position: 41,
                             importance: #MEDIUM } ],
         identification: [ { position: 41 } ] }
  EndDate;

  @UI: { identification: [ { position: 42 } ] }
  BookingFee;

  @UI: { identification: [ { position: 43 } ] }
  TotalPrice;

  @UI: { identification:[ { position: 45,
                            label: 'Comment' } ] }
  Memo;

  @UI: { lineItem:      [ { position: 50,
                            importance: #HIGH },
                          { type: #FOR_ACTION,
                            dataAction: 'set_status_booked',
                            label: 'Set to Booked' } ],
         identification: [ { position: 46, label: 'Travel Status' } ],
         selectionField: [{ position: 40 }],
         textArrangement:   #TEXT_ONLY }
  Status;

  @UI.hidden: true
  StatusText;
}