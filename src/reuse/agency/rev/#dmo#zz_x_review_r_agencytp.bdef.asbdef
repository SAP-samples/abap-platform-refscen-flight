extension using interface /dmo/i_agencytp
implementation in class /dmo/zz_bp_x_review_r_agencytp unique;

extend behavior for /DMO/Agency
{
  event /DMO/AgencyReviewCreated parameter /DMO/ZZ_D_AgencyReviewCreated;

  extend draft determine action Prepare
  {
    validation /DMO/ZZ_Review~/DMO/ratingInRange;
  }
  association /DMO/ZZ_ReviewZAG { create; with draft; }

}

define behavior for /DMO/ZZ_R_Agency_ReviewTP alias /DMO/ZZ_Review using /DMO/ZZ_I_AGENCY_REVIEWTP
with additional save
persistent table /dmo/zz_agn_reva
draft table /dmo/zz_agn_revd
etag master LocalLastChangedAt
lock dependent
authorization dependent
late numbering
{
  update ( features : instance );
  delete ( features : instance );

  field ( readonly ) ReviewID, AgencyID, HelpfulCount, HelpfulTotal, Reviewer;

  action ( authorization : global ) /DMO/reviewWasHelpful    result [1] $self;
  action ( authorization : global ) /DMO/reviewWasNotHelpful result [1] $self;

  validation /DMO/ratingInRange on save { create; field Rating; }

  association _Agency { with draft; }
  mapping for /dmo/zz_agn_reva
    {
      AgencyId           = agency_id;
      ReviewId           = review_id;
      Rating             = rating;
      FreeTextComment    = free_text_comment;
      HelpfulCount       = helpful_count;
      HelpfulTotal       = helpful_total;
      Reviewer           = reviewer;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedAt = local_last_changed_at;
    }

}