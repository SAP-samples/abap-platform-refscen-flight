managed implementation in class /dmo/bp_r_agencytp unique;
strict ( 2 );
with draft;
extensible
{
  with determinations on modify;
  with determinations on save;
  with validations on save;
  with additional save; }

define behavior for /DMO/R_AgencyTP alias /DMO/Agency
persistent table /dmo/agency
draft table /dmo/agency_d query /DMO/R_AgencyDraft
lock master
total etag LastChangedAt
authorization master ( global )
etag master LocalLastChangedAt
late numbering
extensible
{
  create;
  update;
  delete;

  field ( readonly ) AgencyID;
  field ( mandatory ) CountryCode, EMailAddress, Name;

  draft action Resume;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  // Validations for mandatory fields
  validation /DMO/validateEMailAddress on save { create; field EMailAddress; }
  validation /DMO/validateCountryCode on save { create; field CountryCode; }
  validation /DMO/validateName on save { create; field Name; }

  validation /DMO/validateLargeObject on save { create; field Attachment; field MimeType; field Filename; }

  draft determine action Prepare extensible
  {
    validation /DMO/validateEMailAddress;
    validation /DMO/validateCountryCode;
    validation /DMO/validateName;
    validation /DMO/validateLargeObject;
  }

  mapping for /dmo/agency corresponding extensible
    {
      AgencyId           = agency_id;
      Name               = name;
      Street             = street;
      PostalCode         = postal_code;
      City               = city;
      CountryCode        = country_code;
      PhoneNumber        = phone_number;
      EmailAddress       = email_address;
      WebAddress         = web_address;
      Attachment         = attachment;
      MimeType           = mime_type;
      Filename           = filename;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
      LastChangedAt      = last_changed_at;
    }
}