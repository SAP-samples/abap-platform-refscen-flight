@Metadata.layer: #CORE

@UI: { headerInfo: { typeName: 'Travel',
                     typeNamePlural: 'Travels',
                     title: { type: #STANDARD, value: 'TravelID' } },
       presentationVariant: [{ sortOrder: [{ by: 'TravelID', direction: #DESC }], visualizations: [{type: #AS_LINEITEM}]  }] }

annotate entity /DMO/C_Travel_D_D with
{
  @UI.facet: [{ id:            'Travel',
                purpose:       #STANDARD,
                type:          #IDENTIFICATION_REFERENCE,
                label:         'Travel',
                position:      10 },
              { id:            'Booking',
                purpose:       #STANDARD,
                type:          #LINEITEM_REFERENCE,
                label:         'Booking',
                position:      20,
                targetElement: '_Booking'}]
  @UI.hidden: true
  TravelUUID;

  @UI: { lineItem:       [ { position: 10, importance: #HIGH },
                           { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                           { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' },
                           { type: #FOR_ACTION, dataAction: 'deductDiscount', label: 'Deduct Discount', invocationGrouping: #CHANGE_SET } ],
         identification: [ { position: 10, label: 'Travel ID' },
                           { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                           { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' },
                           { type: #FOR_ACTION, dataAction: 'deductDiscount', label: 'Deduct Discount' } ],
         selectionField: [ { position: 10 } ] }
  TravelID;

  @UI: { lineItem:       [ { position: 20, importance: #HIGH } ] ,
         identification: [ { position: 20 } ],
         selectionField: [ { position: 20 } ] }
  AgencyID;

  @UI: { lineItem:       [ { position: 30, importance: #HIGH } ] ,
         identification: [ { position: 30 } ],
         selectionField: [ { position: 30 } ] }
  CustomerID;

  @UI: { lineItem:       [ { position: 40, importance: #MEDIUM } ] ,
         identification: [ { position: 40 } ] }
  BeginDate;

  @UI: { lineItem:       [ { position: 50, importance: #MEDIUM } ] ,
         identification: [ { position: 50 } ] }
  EndDate;

  @UI: { lineItem:       [ { position: 60, importance: #MEDIUM } ] ,
         identification: [ { position: 60 } ] }
  BookingFee;

  @UI: { lineItem:       [ { position: 70, importance: #MEDIUM } ] ,
         identification: [ { position: 70 } ] }
  TotalPrice;

  //  CurrencyCode;

  @UI: { lineItem:       [ { position: 90, importance: #MEDIUM } ] ,
         identification: [ { position: 90 } ] }
  Description;

  @UI: { lineItem:       [ { position: 99, importance: #HIGH } ] ,
         identification: [ { position: 99, label: 'Status' } ],
         selectionField: [{ position: 40 }],
         textArrangement: #TEXT_ONLY }
  OverallStatus;

  @UI.hidden: true
  OverallStatusText;

  @UI.hidden: true
  LocalLastChangedAt;

}